plugins {
	id 'maven-publish'
	id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version '20c7ec554a'
	id "com.diffplug.spotless" version "6.22.0"
}

tasks.named('assemble').get().dependsOn 'spotlessApply'

spotless {
	java {
		target '**/*.java'
		eclipse().configFile("${project.rootDir}/formatter.xml")
		toggleOffOn()
	}
	format 'misc', {
		target '**/*.gitattributes', '**/*.gitignore', '**/*.toml'

		trimTrailingWhitespace()
		endWithNewline()
	}
	yaml {
		target '**/*.yaml'
		jackson()
	}
	json {
		target '**/*.json' 
		gson()
	}
	flexmark {
		target '**/*.md'
		flexmark()
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://maven.fabricmc.net/'
	}
	maven {
		url 'https://maven.architectury.dev/'
	}
	maven {
		url 'https://jitpack.io'
	}
}

preprocess {
	def mc11605_forge  = createNode('1.16.5-forge' , 1_16_05, 'official')
	def mc11605_fabric = createNode('1.16.5-fabric', 1_16_05, 'official')

	def mc11701_forge  = createNode('1.17.1-forge' , 1_17_01, 'official')
	def mc11701_fabric = createNode('1.17.1-fabric', 1_17_01, 'official')

	def mc11802_forge  = createNode('1.18.2-forge' , 1_18_02, 'official')
	def mc11802_fabric = createNode('1.18.2-fabric', 1_18_02, 'official')

	def mc11902_forge  = createNode('1.19.2-forge' , 1_19_02, 'official')
	def mc11902_fabric = createNode('1.19.2-fabric', 1_19_02, 'official')

	def mc11903_forge  = createNode('1.19.3-forge' , 1_19_03, 'official')
	def mc11903_fabric = createNode('1.19.3-fabric', 1_19_03, 'official')

	def mc11904_forge  = createNode('1.19.4-forge' , 1_19_04, 'official')
	def mc11904_fabric = createNode('1.19.4-fabric', 1_19_04, 'official')

	def mc12001_forge  = createNode('1.20.1-forge' , 1_20_01, 'official')
	def mc12001_fabric = createNode('1.20.1-fabric', 1_20_01, 'official')

	def mc12002_forge  = createNode('1.20.2-forge' , 1_20_02, 'official')
	def mc12002_neoforge  = createNode('1.20.2-neoforge' , 1_20_02, 'official')
	def mc12002_fabric = createNode('1.20.2-fabric', 1_20_02, 'official')

	def mc12004_fabric = createNode('1.20.4-fabric', 1_20_04, 'official')
	def mc12004_forge  = createNode('1.20.4-forge' , 1_20_04, 'official')
	def mc12004_neoforge  = createNode('1.20.4-neoforge' , 1_20_04, 'official')

	def mc12006_fabric = createNode('1.20.6-fabric', 1_20_06, 'official')
	def mc12006_forge = createNode('1.20.6-forge', 1_20_06, 'official')
	def mc12006_neoforge  = createNode('1.20.6-neoforge' , 1_20_06, 'official')

	def mc12100_fabric = createNode('1.21-fabric', 1_21_00, 'official')
	def mc12100_forge = createNode('1.21-forge', 1_21_00, 'official')
	def mc12100_neoforge  = createNode('1.21-neoforge' , 1_21_00, 'official')

	def mc12102_fabric = createNode('1.21.2-fabric', 1_21_02, 'official')

	// 1.16.5
	mc11605_fabric.link(mc11605_forge, null)
	mc11701_fabric.link(mc11605_fabric, null)
	// 1.17.1
	mc11701_fabric.link(mc11701_forge, null)
	mc11802_fabric.link(mc11701_fabric, null)
	// 1.18.2
	mc11802_fabric.link(mc11802_forge, null)
	mc11902_fabric.link(mc11802_fabric, null)
	// 1.19.2
	mc11902_fabric.link(mc11902_forge, null)
	mc11903_fabric.link(mc11902_fabric, null)
	// 1.19.3
	mc11903_fabric.link(mc11903_forge, null)
	mc11904_fabric.link(mc11903_fabric, null)
	// 1.19.4
	mc11904_fabric.link(mc11904_forge, null)
	mc12001_fabric.link(mc11904_fabric, null)
	// 1.20.1
	mc12002_fabric.link(mc12001_fabric, null)
	mc12001_fabric.link(mc12001_forge, null)
	// 1.20.2
	mc12004_fabric.link(mc12002_fabric, null)
	mc12002_fabric.link(mc12002_forge, null)
	mc12002_fabric.link(mc12002_neoforge, null)
	// 1.20.4
	mc12006_fabric.link(mc12004_fabric, null)
	mc12004_fabric.link(mc12004_forge, null)
	mc12004_fabric.link(mc12004_neoforge, null)
	// 1.20.6
	mc12100_fabric.link(mc12006_fabric, null)
	mc12006_fabric.link(mc12006_forge, null)
	mc12006_fabric.link(mc12006_neoforge, null)
	// 1.21
	mc12102_fabric.link(mc12100_fabric, null)
	mc12100_fabric.link(mc12100_forge, null)
	mc12100_fabric.link(mc12100_neoforge, null)
	// 1.21.2
	// fabric 1.21.2 is technically here
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
