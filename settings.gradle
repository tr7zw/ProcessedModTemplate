import groovy.json.JsonSlurper

pluginManagement {
    repositories {
        maven { url "https://maven.architectury.dev/" }
		maven {
            name = "tr7zw-proxy"
            url "https://maven.tr7zw.dev/repository/maven-public/"
        }
        gradlePluginPortal()
        mavenCentral()
    }
    resolutionStrategy {
		eachPlugin {
			switch (requested.id.id) {
				case "com.replaymod.preprocess": {
					useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
					break
				}
			}
		}
	}
}

include(":$versionlessname$")

def settings = new JsonSlurper().parseText(file('settings.json').text)
for (String version : settings.versions) {
	include(":$version")

	def proj = project(":$version")
	proj.projectDir = file("versions/$version")
	// use legacy for 1.12.2
	if (version == "1.12.2-forge") {
		proj.buildFileName = "../../legacy.gradle"
	} else {
		proj.buildFileName = "../../common.gradle"
	}
}
